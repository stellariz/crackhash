plugins {
    id 'java'
    id 'org.springframework.boot' version '3.0.3'
    id 'io.spring.dependency-management' version '1.1.0'
}

group = 'ru.nsu.ccfit'
sourceCompatibility = '17'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
            srcDir 'build/generated-sources/jaxb'
        }
    }
}

task genJaxb {
    ext.sourcesDir = "${buildDir}/generated-sources/jaxb"

    outputs.dir sourcesDir

    doLast() {
        project.ant {
            taskdef name: "xjc", classname: "com.sun.tools.xjc.XJCTask",
                    classpath: configurations.jaxb.asPath
            mkdir(dir: sourcesDir)

            xjc(destdir: sourcesDir, package: "ru.nsu.ccfit.schema") {
                arg(value: "-wsdl")
                schema(dir: "src/main/resources", includes: "**/*.xsd")
                produces(dir: sourcesDir, includes: "**/*.java")
            }
        }
    }
}

compileJava.dependsOn genJaxb

configurations {
    jaxb
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    jaxb "org.glassfish.jaxb:jaxb-xjc:4.0.1"
    jaxb "org.glassfish.jaxb:jaxb-runtime:4.0.1"
    implementation "org.glassfish.jaxb:jaxb-runtime:4.0.1"
    implementation 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.0'

    // https://mvnrepository.com/artifact/com.fasterxml.jackson.dataformat/jackson-dataformat-xml
    implementation group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-xml', version: '2.14.2'

    // https://mvnrepository.com/artifact/org.springframework/spring-oxm
    implementation group: 'org.springframework', name: 'spring-oxm', version: '6.0.7'

    // https://mvnrepository.com/artifact/org.apache.commons/commons-collections4
    implementation group: 'org.apache.commons', name: 'commons-collections4', version: '4.4'

    // https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-starter-amqp
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-amqp', version: '3.0.4'

    // https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-starter-data-mongodb
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-mongodb', version: '3.0.5'

    // https://mvnrepository.com/artifact/org.awaitility/awaitility
    implementation group: 'org.awaitility', name: 'awaitility', version: '4.2.0'
}

tasks.named('test') {
    useJUnitPlatform()
}
